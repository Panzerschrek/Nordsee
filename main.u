import "/main_wrapper.u"
import "/stdout.u"
import "/Nordsee/SDL.uh"

pretty_main
{
	ust::stdout_print( "Nordsee\n" );

	unsafe( SDL_Init( SDL_INIT_VIDEO ) );

	auto mut window_title_nt= "Nordsee\0";

	auto SDL_WINDOWPOS_CENTERED= 0x2FFF0000;

	var u32 window_width= 640u, window_height= 480u;

	var $(SDL_Window_) window=
		unsafe( SDL_CreateWindow(
			$<(window_title_nt[0]),
			SDL_WINDOWPOS_CENTERED, SDL_WINDOWPOS_CENTERED,
			i32(window_width), i32(window_height),
			0u ) );

	var $(SDL_Surface) surface_ptr= unsafe( SDL_GetWindowSurface( window ) );

	if( ust::is_nullptr(surface_ptr) ||
		ust::is_nullptr( unsafe( $>(surface_ptr).format ) ) ||
		i32( unsafe( $>( $>(surface_ptr).format ) ).BytesPerPixel ) != 4 )
	{
		ust::stderr_print( "Invalid window surface!\n" );
		return -1;
	}

	var SDL_Surface& surface= unsafe( $>(surface_ptr) );

	var u32 mut iteration= 0u;

	loop label main_loop
	{
		var SDL_Event mut event= zero_init;
		while( unsafe( SDL_PollEvent( $<(event) ) ) != 0 )
		{
			var SDL_EventType t( i64( event.union_contents[0] ) );
			if( t == SDL_QUIT )
			{
				ust::stdout_print( "Quit event\n" );
				break label main_loop;
			}
			if( t == SDL_WINDOWEVENT )
			{
				auto& windows_event= unsafe( cast_ref_unsafe</SDL_WindowEvent/>( event ) );
				if( SDL_WindowEventID( windows_event.event ) == SDL_WINDOWEVENT_CLOSE )
				{
					ust::stdout_print( "Window close event\n" );
					break label main_loop;
				}
			}
			if( t == SDL_KEYDOWN )
			{
				auto& keyboard_event= unsafe( cast_ref_unsafe</SDL_KeyboardEvent/>( event ) );
				if( keyboard_event.keysym.scancode == SDL_SCANCODE_ESCAPE )
				{
					ust::stdout_print( "Esc pressed\n" );
					break label main_loop;
				}
			}
		}

		var bool must_lock_surface= ( surface.flags & u32(SDL_RLEACCEL) ) != 0u;

		if( must_lock_surface )
		{
			unsafe( SDL_LockSurface( surface_ptr ) );
		}

		type Color32= u32;

		var $(Color32) dst_colors= unsafe( ust::byte_ptr_cast</Color32/>( surface.pixels ) );
		var u32 pitch = u32( surface.pitch ) / u32( typeinfo</Color32/>.size_of );
		for( var u32 mut y= 0u; y < u32( surface.h ); ++y )
		{
			var $(Color32) line_dst= unsafe( dst_colors + y * pitch );
			for( var u32 mut x= 0u; x < u32( surface.w ); ++x )
			{
				unsafe( $>( line_dst + x ) )=
					( x * 255u / window_width ) |
					(( y * 255u / window_height ) << 8u) |
					(((iteration << 1u) & 255u) << 16u);
			}
		}
		++iteration;

		if( must_lock_surface )
		{
			unsafe( SDL_UnlockSurface( surface_ptr ) );
		}

		unsafe( SDL_UpdateWindowSurface( window ) );

		unsafe( SDL_Delay( 16u ) );
	}

	unsafe( SDL_DestroyWindow( window ) );

	return 0;
}
