import "/main_wrapper.u"
import "/stdout.u"
import "/Nordsee/SDL.uh"

pretty_main
{
	ust::stdout_print( "Nordsee\n" );

	unsafe( SDL_Init( SDL_INIT_VIDEO ) );

	auto mut window_title_nt= "Nordsee\0";

	auto SDL_WINDOWPOS_CENTERED= 0x2FFF0000;

	var i32 window_width= 640, window_height= 480;

	var $(SDL_Window_) window=
		unsafe( SDL_CreateWindow(
			$<(window_title_nt[0]),
			SDL_WINDOWPOS_CENTERED, SDL_WINDOWPOS_CENTERED,
			window_width, window_height,
			0u ) );

	var $(SDL_Surface) surface_ptr= unsafe( SDL_GetWindowSurface( window ) );

	if( ust::is_nullptr(surface_ptr) ||
		ust::is_nullptr( unsafe( $>(surface_ptr).format ) ) ||
		i32( unsafe( $>( $>(surface_ptr).format ) ).BytesPerPixel ) != 4 )
	{
		ust::stderr_print( "Invalid window surface!\n" );
		return -1;
	}

	var SDL_Surface& surface= unsafe( $>(surface_ptr) );

	loop label main_loop
	{
		var SDL_Event mut event= zero_init;
		while( unsafe( SDL_PollEvent( $<(event) ) ) != 0 )
		{
			var i32 t= i32( i64( event.union_contents[0] ) );
			if( t == SDL_QUIT )
			{
				ust::stdout_print( "Quit event\n" );
				break label main_loop;
			}
			if( t == SDL_WINDOWEVENT )
			{
				auto& windows_event= unsafe( cast_ref_unsafe</SDL_WindowEvent/>( event ) );
				if( i32( windows_event.event ) == SDL_WINDOWEVENT_CLOSE )
				{
					ust::stdout_print( "Window close event\n" );
					break label main_loop;
				}
			}
			if( t == SDL_KEYDOWN )
			{
				auto& keyboard_event= unsafe( cast_ref_unsafe</SDL_KeyboardEvent/>( event ) );
				if( keyboard_event.keysym.scancode == SDL_SCANCODE_ESCAPE )
				{
					ust::stdout_print( "Esc pressed\n" );
					break label main_loop;
				}
			}
		}

		var $(u32) dst_colors= unsafe( ust::byte_ptr_cast</u32/>( surface.pixels ) );
		var u32 pitch = u32( surface.pitch ) / u32( typeinfo</u32/>.size_of );
		for( var u32 mut y= 0u; y < u32( surface.h ); ++y )
		{
			for( var u32 mut x= 0u; x < u32( surface.w ); ++x )
			{
				unsafe( $>( dst_colors + x + y * pitch ) ) = 0x00FF00FFu;
			}
		}

		unsafe( SDL_UpdateWindowSurface( window ) );

		unsafe( SDL_Delay( 100u ) );
	}

	unsafe( SDL_DestroyWindow( window ) );

	return 0;
}
