import "/assert.u"
import "fourier_transform.uh"
import "vec.uh"
import "waves_heightmap.uh"

namespace NS
{

fn GenerateWavesSpectrum( u32 size_log2 ) : ust::vector</Complexf/>
{
	var u32 size= 1u << size_log2;

	var u32 area= size * size;

	var ust::vector</Complexf/> mut freqs( size_type( area ), Complexf::Zero() );

	//freqs[ size_type( 2u | ( 3u << heightmap_size_log2 ) ) ]= Complexf( 5.0f, 0.0f );
	//freqs[ size_type( 7u | ( 5u << heightmap_size_log2 ) ) ]= Complexf( 2.5f, 0.0f );

	// Frequencies above heightmap_size / 2 represent waves traveling in opposite direction.
	//freqs[ size_type( (heightmap_size - 16u ) | ( 16u << heightmap_size_log2 ) ) ]= Complexf( 0.5f, 0.0f );

	var Vec2f mut wind_direction( 1.0f, 2.0f );
	wind_direction.Normalize();

	var f32 max_amplitude= 2.0f;

	for( var u32 mut y= 0u; y < size; ++y )
	{
		for (var u32 mut x= 0u; x < size; ++x )
		{
			var Vec2f k(
				f32( ( x <= (size >> 1u ) ? i32(x) : ( i32(x) - i32(size) ) ) ),
				f32( ( y <= (size >> 1u ) ? i32(y) : ( i32(y) - i32(size) ) ) ) );

			var f32 k2= k.SquareLength();
			if( k2 == 0.0f )
			{
				continue;
			}
			var f32 k4= k2 * k2;

			var f32 dot= k.Dot( wind_direction );

			var f32 scale=
				ust::exp( -1.0f / ( k2 * max_amplitude * max_amplitude ) ) / k4 * ( dot * dot );


			freqs[ size_type( x | (y << size_log2 ) ) ]= Complexf( scale * 5.0f, 0.0f );

		}
	}

	return freqs;
}

fn EvaluateWavesSpectrumInTime(
	u32 heightmap_size_log2,
	ust::array_view_imut</Complexf/> in_freqs,
	ust::array_view_mut</Complexf/> out_freqs,
	f32 time )
{
	assert( in_freqs.size() == out_freqs.size() );

	var u32 heightmap_size= 1u << heightmap_size_log2;
	var u32 half_heightmap_size= heightmap_size >> 1u;

	for( var u32 mut y= 0u; y < heightmap_size; ++y )
	{
		for( var u32 mut x= 0u; x < heightmap_size; ++x )
		{
			// TODO - optimize this.

			// Frequencies above half of the spectrum represent waves traveling backwards.
			var f32 wave_number_x= ( x >= half_heightmap_size ? f32( i32(heightmap_size) - i32(x) ) : f32(x) );
			var f32 wave_number_y= ( y >= half_heightmap_size ? f32( i32(heightmap_size) - i32(y) ) : f32(y) );

			// Calculate wave number for diagonal waves.
			// This formula results from wave length equation for a wave with given x/y lengths.
			var f32 wave_number= ust::sqrt( wave_number_x * wave_number_x + wave_number_y * wave_number_y );

			// See https://en.wikipedia.org/wiki/Gravity_wave
			// omega = sqrt( g * k )
			// g - gravity acceleration, k - wave number
			var f32 omega= ust::sqrt( wave_number );

			var Complexf rotation= Complexf::ImaginaryExponent( time * omega );

			var size_type address( x | (y << heightmap_size_log2) );
			out_freqs[ address ]= in_freqs[ address ] * rotation;
		}
	}
}

fn GenerateWavesHeightmap(
	u32 heightmap_size_log2,
	ust::array_view_imut</Complexf/> freqs,
	ust::array_view_mut</Complexf/> intermediate_buffer,
	ust::array_view_mut</f32/> out )
{
	var u32 heightmap_size= 1u << heightmap_size_log2;

	var u32 heightmap_area= heightmap_size * heightmap_size;

	assert( freqs.size() == out.size() )
	assert( freqs.size() == intermediate_buffer.size() );
	assert( freqs.size() == size_type( heightmap_area ) );

	Perform2DInverseFastFourierTransform( heightmap_size_log2, freqs, intermediate_buffer, out );
}

fn GenerateWavesColors(
	u32 heightmap_size_log2,
	ust::array_view_imut</f32/> heightmap_data,
	ust::array_view_mut</Color32/> out )
{
	assert( heightmap_data.size() == out.size() );
	assert( heightmap_data.size() == ( 1s << ( 2u * heightmap_size_log2 ) ) );

	var f32 mut min_height= heightmap_data.front(), mut max_height= min_height;

	foreach( h : heightmap_data )
	{
		ust::min_assign( min_height, h );
		ust::max_assign( max_height, h );
	}

	var f32 height_range= max_height - min_height;

	foreach( pair : heightmap_data.iter().zip( out.iter() ) )
	{
		var f32 intensity= ust::max( 0.0f, ust::min( ( pair.first - min_height ) / height_range, 1.0f ) );
		var u32 intensity_i( intensity * 255.0f );
		pair.second= intensity_i | (intensity_i << 8u) | (intensity_i << 16u);
	}

	var u32 heightmap_size= 1u << heightmap_size_log2;

	var Color32 red= 0x00FF0000u;
	var Color32 green= 0x0000FF00u;

	for( var u32 mut x= 0u; x < heightmap_size; ++x )
	{
		out[ size_type(x) ]= red;
	}

	for( var u32 mut y= 0u; y < heightmap_size; ++y )
	{
		out[ size_type( y << heightmap_size_log2 ) ]= green;
	}
}

} // namespace NS
